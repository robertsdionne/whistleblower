// room script file

int last_mouse_move_time = 0;
int update_interval = 1; // seconds
int last_mouse_x = 0, last_mouse_y = 0;


function MouseMoved() {
  return mouse.x != last_mouse_x || mouse.y != last_mouse_y;
}


function SayTooltip(String text) {
  cTooltip.x = mouse.x + 64;
  cTooltip.y = mouse.y + 48;
  cTooltip.SayBackground(text);
}


function DisplayTooltip(String text) {
  DisplayAt(mouse.x + 16, mouse.y - 16, 256, text);
  DateTime *now = DateTime.Now;
  last_mouse_move_time = now.RawTime;
}


function repeatedly_execute() {
  DateTime *now = DateTime.Now;
  if (MouseMoved()) {
    last_mouse_move_time = now.RawTime;
  }
  last_mouse_x = mouse.x;
  last_mouse_y = mouse.y;
  if (last_mouse_move_time + update_interval < now.RawTime) {
    Object *hover = Object.GetAtScreenXY(mouse.x, mouse.y);
    if (null != hover) {
      DisplayTooltip(hover.GetTextProperty("description"));
    }
    InventoryItem *hoverItem = InventoryItem.GetAtScreenXY(mouse.x, mouse.y);
    if (DocumentItem1 == hoverItem) {
      String description = "Document";
      if (saved_documents > 1) {
        description = String.Format("%d Documents", saved_documents);
      }
      DisplayTooltip(description);
    } else if (ProfileItem1 == hoverItem) {
      String description = "Profile";
      if (saved_profiles > 1) {
        description = String.Format("%d Profiles", saved_profiles);
      }
      DisplayTooltip(description);
    } else if (null != hoverItem) {
      DisplayTooltip(hoverItem.GetTextProperty("description"));
    }
  }
}


function MailIcon_AnyClick() {
  OpenMail();
  NewMailIcon.Visible = false; // read mail icon
  MailIcon.Visible = true;
}


function DocumentsIcon_AnyClick() {
  OpenDocuments();
}


function WorkIcon_AnyClick() {
  OpenWork();
}


function ChatIcon_AnyClick() {
  OpenChat();
}


function DriveIcon_AnyClick() {
  OpenDrive();
}


function TerminalIcon_AnyClick() {
  OpenTerminal();
}
